import org.apache.tools.ant.filters.ReplaceTokens;
import org.gradle.api.tasks.Exec;
import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'idea'

archivesBaseName = 'openspecimen'

Properties props = new Properties()
props.load(new FileInputStream("build.properties"))

def springVersion    = "4.3.19.RELEASE"
def springSecVersion = "4.2.8.RELEASE"

def deploymentLocation = props.app_home + "/server/default/deploy"

if(new File(props.app_home + '/webapps').exists()) {
  deploymentLocation = props.app_home + "/webapps"
}

project.ext["deploy_location"]           = deploymentLocation

def environment = 'dev';
def envScript;

if (project.hasProperty("env")) {
  environment = env
}

if (environment == 'prod') {
  envScript = file("prod.gradle")
  if (!envScript.exists()) {
    throw new GradleException("Unknown environment: $env")
  }
} else if (environment == 'test') {
  envScript = file("test.gradle")
  if (!envScript.exists()) {
    throw new GradleException("Unknown environment: $env")
  }
} else {
  envScript = file("dev.gradle")
  if (!envScript.exists()) {
    throw new GradleException("Unknown environment: $env")
  }
}

sourceSets {
  main.java.srcDirs = ['WEB-INF/src']
  main.resources.srcDirs = ['WEB-INF/resources']
}

description = "openspecimen"

repositories {
  mavenCentral()
}

configurations {
  ajc
  aspects
  aspectCompile
  
  compile {
    extendsFrom aspects
  }
}

configurations.all {
  transitive = false
}

dependencies {
  ajc       "org.aspectj:aspectjtools:1.8.6"
  compile   "org.aspectj:aspectjrt:1.8.6"
  aspects   "org.springframework:spring-aspects:4.3.19.RELEASE"

  compile fileTree(dir: 'lib', include: ['DynamicExtensions.jar', 'not-yet-commons-ssl-0.3.17.jar', 'os-tinymce.jar'])
  
  // compile group: 'javax', name: 'javaee-api', version: '8.0'
  compile group: 'javax.persistence', name: 'javax.persistence-api', version: '2.2'
  compile group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
  compile group: 'org.antlr', name: 'antlr4', version:'4.5.1'
  compile group: 'aopalliance', name: 'aopalliance', version:'1.0'
  compile group: 'asm', name: 'asm', version:'3.3.1'
  compile group: 'org.aspectj', name: 'aspectjweaver', version:'1.8.6'
  compile group: 'cglib', name: 'cglib', version:'3.1'
  compile group: 'commons-beanutils', name: 'commons-beanutils', version:'1.9.4'
  compile group: 'commons-codec', name: 'commons-codec', version:'1.15'
  compile group: 'commons-collections', name: 'commons-collections', version:'3.2.2'
  compile group: 'commons-dbcp', name: 'commons-dbcp', version:'1.4'
  compile group: 'commons-fileupload', name: 'commons-fileupload', version:'1.3.3'
  compile group: 'commons-httpclient', name: 'commons-httpclient', version:'3.1'
  compile group: 'commons-io', name: 'commons-io', version:'2.10.0'
  compile group: 'commons-lang', name: 'commons-lang', version:'2.6'
  compile group: 'commons-logging', name: 'commons-logging', version:'1.2'
  compile group: 'org.apache.commons', name: 'commons-digester3', version:'3.2'
  compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.4'
  compile group: 'org.apache.taglibs', name: 'taglibs-standard-impl', version: '1.2.5'
  compile group: 'org.apache.tika', name: 'tika-parsers', version: '1.27'
  compile group: 'net.sf.kxml', name: 'kxml2-min', version:'2.3.0'
  compile group: 'net.sf.ehcache', name: 'ehcache', version:'1.2.3'
  compile group: 'net.sf.opencsv', name: 'opencsv', version:'2.3'
  compile group: 'com.google.code.gson', name: 'gson', version:'2.3.1'
  compile group: 'org.hibernate.common', name: 'hibernate-commons-annotations', version:'5.1.2.Final'
  compile group: 'org.hibernate', name: 'hibernate-core', version:'5.4.32.Final'
  compile group: 'org.hibernate', name: 'hibernate-envers', version:'5.4.32.Final'
  compile group: 'net.bytebuddy', name: 'byte-buddy', version: '1.8.22'
  compile group: 'com.fasterxml', name: 'classmate', version: '1.4.0'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.12.4'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version:'2.12.4'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.12.4'
  compile group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version:'1.9.13'
  compile group: 'org.codehaus.jackson', name: 'jackson-core-asl', version:'1.9.13'
  compile group: 'org.javassist', name: 'javassist', version:'3.19.0-GA'
  compile group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0.1'
  compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version:'2.3.0.1'
  compile group: 'com.sun.xml.bind', name: 'jaxb-xjc', version:'2.3.0.1'
  compile group: 'com.sun.xml.bind', name: 'jaxb-libs', version:'1.0.6'
  compile group: 'org.jboss.logging', name: 'jboss-logging', version:'3.3.2.Final'
  compile group: 'org.scijava', name: 'jep', version:'2.4.2'
  compile group: 'org.json', name: 'json', version:'20140107'
  compile group: 'javax.ws.rs', name: 'jsr311-api', version:'1.1.1'
  compile group: 'javax.transaction', name: 'jta', version:'1.1'
  compile group: 'com.sun.mail', name: 'javax.mail', version: '1.6.2'
  compile group: 'org.liquibase', name: 'liquibase-core', version:'3.8.9'
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
  compile group: 'log4j', name: 'log4j', version:'1.2.16'
  compile group: 'log4j', name: 'apache-log4j-extras', version: '1.2.17'
  compile group: 'quartz', name: 'quartz', version:'1.5.2'

  compile group: 'org.springframework', name: 'spring-aop', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-beans', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-context', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-context-support', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-core', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-expression', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-instrument', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-jdbc', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-tx', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-web', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-webmvc', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-orm', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-messaging', version: '4.3.30.RELEASE'
  compile group: 'org.springframework', name: 'spring-jms', version: '4.3.30.RELEASE'
  compile group: 'org.springframework.integration', name: 'spring-integration-core', version: '4.3.24.RELEASE'
  compile group: 'org.springframework.integration', name: 'spring-integration-mail', version: '4.3.24.RELEASE'
  compile group: 'org.springframework.ldap', name: 'spring-ldap-core', version:'2.3.2.RELEASE'

  compile group: 'org.springframework.security', name: 'spring-security-config', version: '4.2.20.RELEASE'
  compile group: 'org.springframework.security', name: 'spring-security-core', version: '4.2.20.RELEASE'
  compile group: 'org.springframework.security', name: 'spring-security-ldap', version: '4.2.20.RELEASE'
  compile group: 'org.springframework.security', name: 'spring-security-web', version: '4.2.20.RELEASE'

  // compile group: 'velocity', name: 'velocity-dep', version:'1.4'
  compile group: 'com.thoughtworks.xstream', name: 'xstream', version:'1.4.17'
  compile group: 'com.itextpdf', name: 'itextpdf', version:'5.5.13.2'
  compile group: 'dom4j', name: 'dom4j', version: '1.6.1'
  compile group: 'com.jcraft', name: 'jsch', version: '0.1.54'

  compile group: 'com.github.ua-parser', name: 'uap-java', version: '1.5.2'
  compile group: 'org.yaml', name: 'snakeyaml', version: '1.28'

  compile group: 'org.owasp.encoder', name: 'encoder', version: '1.2.3'

  // HTML to PDF lib
  compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.24'
  compile group: 'org.apache.pdfbox', name: 'xmpbox', version: '2.0.24'
  compile group: 'org.apache.pdfbox', name: 'fontbox', version: '2.0.24'
  compile group: 'de.rototor.pdfbox', name: 'graphics2d', version: '0.32'
  compile group: 'com.openhtmltopdf', name: 'openhtmltopdf-core', version: '1.0.9'
  compile group: 'com.openhtmltopdf', name: 'openhtmltopdf-pdfbox', version: '1.0.9'

  compile group: 'com.google.zxing', name: 'core', version: '3.4.1'


  //SAML dependencies lib
  compile group: 'org.springframework.security.extensions', name: 'spring-security-saml2-core', version:'1.0.10.RELEASE'
  compile group: 'org.opensaml', name: 'opensaml', version: '2.6.4'
  compile group: 'org.opensaml', name: 'openws', version: '1.5.4'
  compile group: 'org.opensaml', name: 'xmltooling', version: '1.4.4'
  compile group: 'xml-security', name: 'xmlsec', version: '1.3.0'
  compile group: 'xml-apis', name: 'xml-apis', version: '1.4.01'
  compile group: 'org.owasp.esapi', name: 'esapi', version: '2.1.0.1'
  compile group: 'xerces', name: 'xercesImpl', version: '2.12.1'
  compile group: 'xalan', name: 'xalan', version: '2.7.2'
  compile group: 'xalan', name: 'serializer', version: '2.7.2'
  compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.69'
  compile group: 'joda-time', name: 'joda-time', version: '2.10'
  compile group: 'org.apache.velocity', name: 'velocity', version:'1.7'

  runtime group: 'antlr', name: 'antlr', version: '2.7.7'
  runtime group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.12'
  
  testCompile group: 'junit', name: 'junit', version:'4.11'
}

compileJava {
  doLast{
    ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", 
                classpath: configurations.ajc.asPath)
      
    ant.iajc(
      source: 1.8,
      target: 1.8,
      destDir: sourceSets.main.output.classesDir.absolutePath, 
      maxmem: "512m", 
      fork: "true",
      aspectPath: configurations.aspects.asPath,  
      sourceRootCopyFilter: "**/*.java",
      classpath: "${configurations.compile.asPath};${configurations.aspectCompile.asPath}"){
      sourceroots{
        sourceSets.main.java.srcDirs.each{
          pathelement(location:it.absolutePath)
        }
      }
    }
  }
}

def getTodayDate() {
  def today = new Date()
  return String.valueOf(today.getTime())
}

def getCommitRevision() {
  def revCmd = "git rev-parse --short HEAD"
  def revision = revCmd.execute()
  return revision.text.trim()
}

def getBranch() {
  def branchCmd = "git symbolic-ref -q --short HEAD";
  def branch = branchCmd.execute().text.trim();

  if (branch.length() == 0) {
    branchCmd = "git describe --tags --exact-match";
    branch = branchCmd.execute().text.trim();
  }
  return branch;
}

processResources {
  from(sourceSets.main.resources.srcDirs) {
    include 'application.properties'

    filter(
      ReplaceTokens, 
      tokens: [
        buildinfo_version : getBranch(),
        buildinfo_date : getTodayDate(),
        buildinfo_commit_revision : getCommitRevision()])
  }
}

task unzip_form_designer(type: Copy) {
  def zipFile = file('./form-designer.zip')
  def outputDir = file("${buildDir}/form-designer")
  
  from zipTree(zipFile) 
  into outputDir
}

task create_war(type: War) {
  from ('./') {
    include 'ngweb/**'
  }
  
  from ('./www/dist/') {
    include '**/**'
  }
  
  from ('./WEB-INF/') {
    include 'resources/**'
    include 'applicationContext.xml'
    include 'restApiContext.xml'
    include 'pluginUiContext.xml'
    include 'migrationContext.xml'
    include 'postInitContext.xml'
    include 'web.xml'
    
    into 'WEB-INF/'
  }
  
  from ('./build/') {
    include 'form-designer/**'
  }

  from ('./ui/dist') {
    include '**/**'

    into 'vue-app/'
  }
  
  manifest {
    attributes(
      "Implementation-Title": archivesBaseName,
      "Implementation-Version": getBranch(),
      "Implementation-Timestamp": new Date()
    )
  }
}

def grunt_cmd = "grunt"

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  grunt_cmd = "grunt.cmd"
}

task grunt_build(type: Exec) {
  workingDir './www'
  commandLine grunt_cmd, 'build'
}

task vue_backup_src(type: Exec) {
  workingDir './ui'
  commandLine 'mv', 'src', 'src_bk'
}

task vue_install_dependencies(type: Exec) {
  workingDir './ui'
  commandLine 'npm', 'install'
}

task vue_restore_src(type: Exec) {
  workingDir './ui'
  commandLine 'mv', 'src_bk', 'src'
}

task vue_build(type: Exec) {
  workingDir './ui'
  commandLine 'npm', 'run', 'build'
}

vue_install_dependencies.dependsOn vue_backup_src
vue_restore_src.dependsOn vue_install_dependencies
vue_build.dependsOn vue_restore_src
create_war.dependsOn unzip_form_designer, grunt_build, vue_build
build.dependsOn create_war

apply from: envScript
